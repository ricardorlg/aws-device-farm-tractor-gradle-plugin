plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.30'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'maven-publish'
    id 'java-gradle-plugin'
}

group = 'com.ricardorlg.devicefarm'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/ricardorlg/aws-device-farm-tractor")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
            password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation platform('software.amazon.awssdk:bom:2.15.82')
    implementation "com.ricardorlg.devicefarm:tractor:$tractor_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "io.arrow-kt:arrow-core:$arrow_version"

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.7.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.7.1")

}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes("Implementation-Title": "Device Farm Tractor gradle plugin",
                "Implementation-Version": project.version.toString())
    }
}

pluginBundle {
    website = 'https://github.com/ricardorlg/aws-device-farm-tractor-gradle-plugin'
    vcsUrl = 'https://github.com/ricardorlg/aws-device-farm-tractor-gradle-plugin'
    tags = ['appium', 'Serenity BDD', 'AWS device famr']
}

gradlePlugin {
    plugins {
        deviceFarmTractorGradlePlugin {
            id = "com.ricardorlg.devicefarm.DeviceFarmTractorGradlePlugin"
            displayName = 'AWS Device farm tractor gradle plugin'
            description = 'Run Serenity BDD appium tests on AWS Device farm'
            implementationClass = 'com.ricardorlg.devicefarm.tractor.gradle.DeviceFarmTractorGradlePlugin'
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ricardorlg/aws-device-farm-tractor-gradle-plugin")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications{
        pluginPublication (MavenPublication) {
            from    components.java
        }
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}

compileTestKotlin {
    kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
}